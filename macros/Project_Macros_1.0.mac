// Macro file

// ---------------- MACRO COMMANDS ----------------
// Splash Screen            *SPL <number>
// Clear Screen             Z
// Display Bitmap Image     xi <index> x y
// Clear All Hotspot        xc all
// Clear Hotspot            xc <n>
// Define Hotspot           x <n> x0 y0 x1 y1
// Wait                     w <number of milliseconds>
// Touch Macro Assign Quiet xmq <touch index> <macro index | name> [<macro2 index>]
// Output String (Main)     out “<text string>”
// Set Font                 f <fontName>
// Set Color 				s <fore> <back>
// Text Display             t “text string” x y T
// Set Text Alignment 		ta [L|C|R][T|C|B]
// External Backlight  		xbb [+|-]<level>
// 		Bright Ctrl
// Define Max, Min 			*maxBrite <value from 0 to 255>
//		Brightness 			*minBrite <value from 0 to 255>
// ------------------------------------------------

// ------------ BITMAP IDX ------------
// IDX      NOTES
// 1        Splash page
// 2        Temperature page
// 3        Laser page
// 4        Motor page
// 
// ------------------------------------

*minBrite 0
*maxBrite 255

// ---------------------------- Splash Page - TOP -----------------------------------
#define show_splash
xi 1 0 0
out "load\r"
#end


#define load_bar_full
m load_bar_once `0` `1` `2` `3`
m load_bar_once `0` `1` `2` `3`
m load_bar_once `0` `1` `2` `3`
m load_bar_once `0` `1` `2` `3`
m load_bar_once `0` `1` `2` `3`
out "endload\r"
#end

#define load_bar_once
xi `(`3`+0)` `0` `1`
w `2`
xi `(`3`+1)` `0` `1`
w `2`
xi `(`3`+2)` `0` `1`
w `2`
xi `(`3`+3)` `0` `1`
w `2`
xi `(`3`+4)` `0` `1`
w `2`
xi `(`3`+5)` `0` `1`
w `2`
xi `(`3`+6)` `0` `1`
w `2`
xi `(`3`+7)` `0` `1`
w `2`
xi `(`3`+8)` `0` `1`
w `2`
xi `(`3`+9)` `0` `1`
w `2`
xi `(`3`+10)` `0` `1`
w `2`
xi `(`3`+11)` `0` `1`
w `2`
xi `(`3`+0)` `0` `1`
w `2`
#end
// ---------------------------- Splash Page - BOTTOM -----------------------------------


// ---------------------------- Login Page - TOP ---------------------------------------
#define display_login_page
z                       // Clear Screen
xi 2 0 0                // Login page bitmap
m define_hotspots_login
#end

#define define_hotspots_login
xc all

x 131 205 174 264 233   // 1
xmq 131 ONE             // 1

x 132 285 174 344 233   // 2
xmq 132 TWO             // 2

x 133 365 174 424 233   // 3
xmq 133 THREE           // 3

x 134 205 252 264 311   // 4
xmq 134 FOUR            // 4

x 135 285 252 344 311   // 5
xmq 135 FIVE            // 5

x 136 365 252 424 311   // 6
xmq 136 SIX             // 6

x 137 205 329 264 388   // 7
xmq 137 SEVEN           // 7

x 138 285 329 344 388   // 8
xmq 138 EIGHT           // 8

x 139 365 329 424 388   // 9
xmq 139 NINE            // 9

x 128 205 406 264 465   // *
xmq 128 STAR		// *

x 129 285 406 344 465   // 0
xmq 129 ZERO            // 0

x 130 365 406 424 465   // #
xmq 130 POUND           // #
#end

#define ONE
out "lp_1\r"
#end

#define TWO
out "lp_2\r"
#end

#define THREE
out "lp_3\r"
#end

#define FOUR
out "lp_4\r"
#end

#define FIVE
out "lp_5\r"
#end

#define SIX
out "lp_6\r"
#end

#define SEVEN
out "lp_7\r"
#end

#define EIGHT
out "lp_8\r"
#end

#define NINE
out "lp_9\r"
#end

#define ZERO
out "lp_0\r"
#end

#define STAR
out "lp_*\r"
#end

#define POUND
out "lp_#\r"
#end

#define display_asterik_1
xi 39 187 109
#end

#define display_asterik_2
xi 39 260 109
#end

#define display_asterik_3
xi 39 335 109
#end

#define display_asterik_4
xi 39 409 109
#end

#define power_off_button
v off
z
xs 159 0 0 640 480
xmq 159 power_on
#end

#define power_on
v on
xc all
m show_splash
#end

#define display_login_attempts_left
m display_login_page
f 16B
s 0 0
t "You have" 40 200 T
s 5 0
t "`0`" 145 200 T
s 0 0
t "attempt(s) left" 30 240 T
#end

#define display_locked_page
m display_login_page
bc 5
f 32B
s 5 0
t "LOCKED OUT" 180 50 T
f 16B
t "ADMIN LOGIN" 30 200 T
t "REQUIRED" 40 240 T
#end

// ---------------------------- Login Page - BOTTOM --------------------------------------


// ---------------------------- Main Page - TOP ---------------------------------------
// Parameters: `0`->(User ID)
#define display_main_page
xc all
xi 34 0 0

bd 25 0 0 1 "" 0 0 41 41
xm 25 logout_button

bd 29 75 10 1 "" 0 0 42 42
xm 29 setting_button

// Define the hotspots
xs 140 169 131 476 211
xmq 140 temp_hotspot

xs 141 169 233 477 315
xmq 141 motor_hotspot

xs 142 170 343 477 425
xmq 142 laser_hotspot

// Display the User ID
m display_uid

#end
// ---------------------------- MainPage - BOTTOM ------------------------------------

#define logout_button
bc 25
out "mp_logout\r"
#end

#define setting_button
bc 29
out "mp_setting\r"
#end

#define temp_hotspot
out "mp_temp\r"
#end

#define motor_hotspot
out "mp_motor\r"
#end

#define laser_hotspot
out "mp_laser\r"
#end

// ---------------------------- Lockout Page - TOP ---------------------------------------
// ---------------------------- Lockout Page - BOTTOM ------------------------------------


// ---------------------------- Temperature Page - TOP ------------------------------------

#define display_temp_page
xc all
z
xi 35 0 0

// Display the User ID
m display_uid
#end

// ---------------------------- Temperature Page - BOTTOM ---------------------------------

#define temp_unit_f
out "ns_f\r"
xc all
xi 211 157 104
// Set hotspots
xs 145 157 104 509 306
xmq 145 temp_unit_c
bd 26 0 0 1 "" 0 0 15 15
xm 26 main_button
#end

#define temp_unit_c
out "ns_c\r"
xc all
xi 210 157 104
// Set hotspots
xs 144 157 104 509 306
xmq 144 temp_unit_f
bd 26 0 0 1 "" 0 0 15 15
xm 26 main_button
#end

#define main_button
out "mp_main\r"
#end

// ---------------------------- Motor Page - TOP --------------------------------------
#define display_motor_page
// Clear everything
xc all
z
xi 35 0 0

// Display the User ID
m display_uid

// Define the main page button assign the macro to it
bd 26 0 0 1 "" 0 0 15 15
xm 26 main_button
#end
// ---------------------------- Motor Page - BOTTOM ------------------------------------

// ---------------------------- Laser Page - TOP --------------------------------------
#define display_laser_page
// Clear everything
xc all
S FFFFFF 000000
z
set s2 470
set s3 1
xs 155 120 0 520 239
xmq 155 speed_up
xs 156 120 240 520 480
xmq 156 speed_down

// Display the User ID
m display_uid

// Define the main page button assign the macro to it
bd 26 0 0 1 "" 0 0 15 15
xm 26 main_button
#end
// ---------------------------- Laser Page - BOTTOM ------------------------------------

#define text_display
xi 50 120 `s2`
set s2 `(`s2`-`s3`)`
#end

#define speed_up
set s3 `(`s3`+1)`
#end

#define speed_down
set s3 `(`s3`-1)`
#end

//----------------------------  Helper Functions ---------------------------------------
// Parameters: `0`->(User ID "First name initial") `1`->(User ID "Last name")
#define set_uid 
set s0 "`0` `1`"
#end

#define display_uid
f 14B
s 8 0
ta RT
t "`s0`" 639 1 T
#end

//----------------------------  Settings Sreen ---------------------------------------
// `0` -> brightness || `1` -> contrast
#define display_settings_screen
xc all
z
xi 36 0 0
set e0 241					// Set default starting index of sliders
set e1 141					// Set index of first background slider bmp
m display_slider 227 152 `0`
m display_slider 227 244 `1`
m display_slider 227 341 255
m define_hotspots_date_time 163 393 228 433
m define_hotspots_time 163 437 236 477

// Display the User ID
m display_uid

// Define the main page button assign the macro to it
bd 26 0 0 1 "" 0 0 15 15
xm 26 main_button
#end

// `0` -> x || `1` -> y || `2` -> slider position
// e0 increments && e1 increments || then resets when refresh settings screen
#define display_slider
sl `e0` `e1` `0` `1` 38 16 1 0 1 255 1
sv `e0` `2`
set e0 `(`e0`+1)`
set e1 `(`e1`+1)`
#end

#define adjust_brightness
xbb `0`
#end

//--------------------------  Settings Date/Time -----------------------------------

// `0` -> slider position || `1` -> MAX || `2` -> MIN
#define display_date_time_slider
sl 243 143 227 341 38 16 1 0 1 `1` `2`
sv 243 `0`
#end

// `0` -> x0 || `1` -> y0 || `2` -> x1 || `3` -> y1
#define define_hotspots_date_time
xs 150 `0` `1` `2` `3`   				// Month
xmq 150 MONTH           				// Month

xs 151 `(`0`+76)` `1` `(`2`+83)` `3`			// Day
xmq 151 DAY           					// Day

xs 152 `(`0`+149)` `1` `(`2`+202)` `3`			// Year
xmq 152 YEAR           					// Year
#end

// `0` -> x0 || `1` -> y0 || `2` -> x1 || `3` -> y1
#define define_hotspots_time
xs 153 `0` `1` `2` `3`  				// Hour
xmq 153 HOUR           					// Hour

xs 154 `(`0`+74)` `1` `(`2`+104)` `3`			// Minute
xmq 154 MINUTE         					// Minute

xs 155 `(`0`+178)` `1` `(`2`+194)` `3`			// Second
xmq 155 SECOND          				// Second

xs 156 `(`0`+268)` `1` `(`2`+262)` `3`   		// AM / PM
xmq 156 AMPM           					// AM / PM
#end

#define MONTH
out "dt_Month\r"
#end

#define DAY
out "dt_day\r"
#end

#define YEAR
out "dt_year\r"
#end

#define HOUR
out "dt_hour\r"
#end

#define MINUTE
out "dt_minute\r"
#end

#define SECOND
out "dt_second\r"
#end

#define AMPM
out "dt_ampm\r"
#end


//------------------ Game Page - TOP -------------------
#define display_game_page
xc all
z
xi 45 0 0
xi 46 0 115
xi 47 120 115
x 149 258 348 379 390
xmq 149 start

//Display message
f 24B
s 5 0
t "The Breaking Stone" 160 130 T
f 16B
s 0 0
t "Use your finger as a laser beam to" 150 180 T
t "break all the kidney stones which" 150 210 T
t "appear on the patient's kidneys." 150 240 T
f 12B
t "Note: you'll need at least 10 points" 190 290 T
t "to save your patient's life" 236 310 T
f 24B
t "START" 263 350 T

// Display the User ID
m display_uid

// Define the main page button assign the macro to it
bd 26 0 0 1 "" 0 0 15 15
xm 26 main_button
#end

// ---------------------------- Game Page - BOTTOM ------------------------------------

// `0` -> x || `1` -> y || `2` -> stone duration

#define stone_display
xi 46 0 115
bd 30 `0` `1` 1 "" 0 0 43 44
xm 30 add_point
w `2`
bc 30
xi 46 0 115
#end

#define add_point
bc 30
out "get_point\r"
#end

//------------------win----------------------
// Parameters: `0`->(User ID "First name initial") `1`->(User ID "Last name")
#define win
xi 47 120 115
x 149 258 348 379 390
xmq 149 start

//Display message
f 24B
s 5 0
t "YOU WIN" 245 130 T
f 16B
s 0 0
t "You have broken enough kidney" 150 180 T
t "stones and saved your patient's life." 150 210 T
t "Well done, Dr. `1`" 150 280 T
f 24B
t "RETRY" 262 350 T
#end

//--------------lose----------------------
// Parameters: `0`->(hours) `1`->(minutes) `2`->(AM/PM)
#define lose
xi 47 120 115
x 149 258 348 379 390
xmq 149 start
xi 48 316 405
xi 48 238 425

//Display message
f 24B
s 5 0
t "YOU LOSE" 235 130 T
f 16B
s 0 0
t "The kidney stones have caused both" 128 170 T
t "urinary tract obstructions and infection" 123 200 T
t "You have lost your patient." 128 230 T
s 5 0
t "Time of death: `0`:`1``2`" 200 270 T
s 0 0
f 12B
t "(Too hard? Try to warm up the patient's body temp)" 126 310 T
f 24B
s 0 0
t "RETRY" 262 350 T
#end

#define start
xc all
xi 45 0 0

// Display the User ID
m display_uid

// Define the main page button assign the macro to it
bd 26 0 0 1 "" 0 0 15 15
xm 26 main_button

out "start\r"
#end